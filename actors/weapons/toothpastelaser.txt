ACTOR ToothpasteProjectile : FastProjectile
{
	Scale 0.3
	Damage (4)
	Speed 60
	Projectile
	+SEEKERMISSILE
	+SCREENSEEKER
	+SKYEXPLODE
	+RIPPER
	RenderStyle Add
	Alpha 0.8
    Decal "ToothpasteLightning"
	MissileType "ToothpasteTrail"
	MissileHeight 8
	Translation "0:255=%[0.00,0.00,0.00]:[0.00,2.00,1.00]"
	States
	{
	Spawn:
		SLSS ABC 1 BRIGHT
	SpawnLoop:
		SLSS DEFABC 1 Bright A_SeekerMissile(2, 2, SMF_PRECISE|SMF_LOOK, 256)
		Loop
	Death:
		SLSE ABCDEF 2 Bright
		Stop
	}
}

ACTOR ToothpasteProjectileAltfire : ToothpasteProjectile
{
	-RIPPER
	-SEEKERMISSILE
	-SCREENSEEKER
	Damage (20)
	States
	{
	Spawn:
		SLSS ABCDEF 1 BRIGHT
		Loop
	}
}

ACTOR SuperToothpasteProjectile : ToothpasteProjectile
{
	Scale 1.0
	Damage (30)
	Speed 70
	MissileType "SuperToothpasteTrail"
	MissileHeight 12
    Decal "BigToothpasteLightning"
	-SEEKERMISSILE
	-SCREENSEEKER
	+EXTREMEDEATH
	States
	{
	Spawn:
		SLSS ABCDEF 1 Bright
		Loop
	}
}

ACTOR ToothpasteTrail
{
	RenderStyle Add
	Alpha 0.8
	Scale 0.3
	+NoGravity
	Translation "0:255=%[0.00,0.00,0.00]:[0.00,2.00,1.00]"
	States
	{
	Spawn:
		SLSS ABCDE 2 Bright
		SLSS FABCDE 1 Bright A_FadeOut(0.06)
		Wait
	}
}

ACTOR SuperToothpasteTrail : ToothpasteTrail
{
	Scale 1.0
}

ACTOR ToothpasteCharge : Ammo
{
	Inventory.MaxAmount 100
}

ACTOR ToothpasteLaser : ColaWeapon replaces BFG9000
{
	Weapon.SlotNumber 7
	Weapon.AmmoType1 "ToothpasteCharge"
	Weapon.AmmoType2 "ToothpasteCharge"
	Inventory.PickupMessage "Toothpaste Laser - \"Dealing with plaque (and demons), the sci-fi way!\" (Slot 7)"
	Inventory.PickupSound "Pickups/GetToothpaste"
	Scale 1.2
	Tag "Toothpaste Laser"
    Inventory.Icon "TPPKA0"
	States
	{
	Ready:
		TPLG A 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
	Deselect:
		TPLG A 1 {
			A_Lower;
			A_Lower;
			}
		Loop
	Select:
		TPLG A 1 {
			A_Raise;
			A_Raise;
		}
		Loop
	Fire:
		TPLG A 1 { if (CountInv("ToothpasteCharge") < 1) { A_PlaySound("ToothpasteLaser/Charge", CHAN_WEAPON); } }
		TPLG C 1 {
			if (CountInv("ToothpasteCharge") == 100) {
				return state("FireForReal");
			}
			else if (CountInv("ToothpasteCharge") >= 75) {
				A_Overlay(2, "OverlayChargeFull");
				A_WeaponOffset(random(-6, 6), random(32, 40), WOF_INTERPOLATE);
				A_GiveInventory("ToothpasteCharge", 1 + ( CountInv("PowerWeaponLevel2") * 2 ) );
			}
			else if (CountInv("ToothpasteCharge") >= 33) {
				A_Overlay(2, "OverlayChargeMedium");
				A_WeaponOffset(random(-6, 6), random(32, 40), WOF_INTERPOLATE);
				A_PlaySound("alienblaster/ready", CHAN_5);
				A_GiveInventory("ToothpasteCharge", 1 + ( CountInv("PowerWeaponLevel2") * 2 ) );
			}
			else if (CountInv("ToothpasteCharge") < 33) {
				A_Overlay(2, "OverlayChargeLow");
				A_WeaponOffset(random(-3, 3), random(32, 36), WOF_INTERPOLATE);
				A_GiveInventory("ToothpasteCharge", 1 + ( CountInv("PowerWeaponLevel2") * 2 ) );
			}
			else {
				A_WeaponOffset(random(-1, 1), random(32, 34), WOF_INTERPOLATE);
				A_GiveInventory("ToothpasteCharge", 1 + ( CountInv("PowerWeaponLevel2") * 2 ) );
			}
			return state("RefireCheck");
		}
	RefireCheck:
		TPLG A 1 A_Refire
	FireForReal:
		TPLG B 2 BRIGHT {
			if (CountInv("ToothpasteCharge") >= 75) {
				A_PlaySound("ToothpasteLaser/firehigh", CHAN_WEAPON);
				A_GunFlash;
				A_AlertMonsters;
				return state("FireHeavy");
			}
			else if (CountInv("ToothpasteCharge") >= 33) {
				A_PlaySound("ToothpasteLaser/firemedium", CHAN_WEAPON);
				A_GunFlash;
				A_AlertMonsters;
				return state("FireMedium");
			}
			else {
				A_PlaySound("ToothpasteLaser/firelow", CHAN_WEAPON);
				A_GunFlash;
				A_AlertMonsters;
				return state("FireLow");
			}
			return state("Null");
		}
	FireHeavy:
		TPLG A 0 A_FireCustomMissile("SuperToothpasteProjectile", 0, 0, 6)
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", -5, 0, 6, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM, -2.5)
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", -5, 0, 6, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM, 2.5)
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", 5, 0, 6, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM, -2.5)
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", 5, 0, 6, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM, 2.5)
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", -6, 0, 6, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM)
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", 6, 0, 6, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM)
		"####" # 0 A_Overlay(1001, "BarrelHugeSmokeOverlay")
		Goto FireFinish
	FireMedium:
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", -4, 0, 6, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM, -2.5)
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", -4, 0, 6, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM, 2.5)
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", 4, 0, 6, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM, -2.5)
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", 4, 0, 6, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM, 2.5)
		"####" # 0 A_Overlay(1001, "BarrelSmokeOverlay")
		Goto FireFinish
	FireLow:
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", -4, 0, 7, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM)
		TPLG A 0 A_FireCustomMissile("ToothpasteProjectile", 4, 0, 7, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM)
		Goto FireFinish
	FireFinish:
		TPLG A 0 A_TakeInventory("ToothpasteCharge", 999)
		/*
		TPLG E 1 A_WeaponOffset(2.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(4.0, 4.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(8.0, 8.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(10.0, 10.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(9.0, 9.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(7.0, 7.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(5.0, 5.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(3.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(1.0, 1.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(-2.0, -2.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(-4.0, -4.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(-6.0, -6.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(-10.0, -10.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(-8.0, -8.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(-6.0, -6.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(-4.0, -4.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(-2.5, -2.5, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(-1.5, -1.5, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(0.0, 32.0, WOF_INTERPOLATE)
		*/
		TPLG E 1 A_WeaponOffset(0, -2.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, -4.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, -8.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, -10.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, -9.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, -7.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, -5.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, -3.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, 1.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, 4.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, 6.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, 10.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(0, 8.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(0, 6.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(0, 4.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(0, 2.5, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(0, 1.5, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1
		Goto Ready
	Altfire:
		TPLG E 1 BRIGHT {
			A_GunFlash;
			A_AlertMonsters;
			A_PlaySound("ToothpasteLaser/Byoo", CHAN_WEAPON);
			A_FireCustomMissile("ToothpasteProjectileAltfire", 0, 0, 7, 0, FPF_AIMATANGLE|FPF_NOAUTOAIM);
		}
		/*
		TPLG E 1 A_WeaponOffset(2.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(4.0, 4.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(-2.5, -2.5, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(-1.5, -1.5, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(0.0, 32.0, WOF_INTERPOLATE)
		*/
		TPLG E 1 A_WeaponOffset(0, -2.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG E 1 A_WeaponOffset(0, -4.0, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(0, -2.5, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1 A_WeaponOffset(0, -1.5, WOF_ADD|WOF_INTERPOLATE)
		TPLG A 1
		Goto Ready
	OverlayChargeLow:
		TPLD A 1
		Stop
	OverlayChargeMedium:
		TPLD B 1
		Stop
	OverlayChargeFull:
		TPLD C 1
		Stop
	BarrelSmokeOverlay:
		TNT1 A 10
		"####" # 0 A_JumpIf(GetCVAR("colavr_weapons_smoke")==false, 2)
		"####" # 0 A_FireProjectile("RocketBarrelSmoke",0,0,0,0,FPF_NOAUTOAIM,0)
		"####" # 0
		Stop
	BarrelHugeSmokeOverlay:
		TNT1 A 10
		"####" # 0 A_JumpIf(GetCVAR("colavr_weapons_smoke")==false, 2)
		"####" # 0 A_FireProjectile("PlasmaBarrelSmoke2",0,0,0,0,FPF_NOAUTOAIM,0)
		"####" # 0
		Stop
	Spawn:
		TPPK A -1
		Stop
	}
}