ACTOR ColaHandgunSelected : Inventory {}

ACTOR ColaHandgunLevel : Inventory
{
	Inventory.MaxAmount 3
}

ACTOR ColaHandgunExp : Ammo
{
	Inventory.MaxAmount 15
	+IGNORESKILL
}

ACTOR ColaHandgunClip : Ammo
{
	Inventory.MaxAmount 12
	+INVENTORY.IGNORESKILL
}

ACTOR TracerBulletPistol : TracerBullet
{
	Speed 120
	Damage (12)
	//MissileType TracerTrailBig
	MissileType None
	Scale 0.5
	States
	{
	Spawn:
		TRAC B 1 BRIGHT
		Loop
	}
}

ACTOR TracerTrailBig : TracerTrail
{
	Scale 0.5
	States
	{
	Spawn:
		TRAC B 1 BRIGHT A_FadeOut(0.3)
		Loop
	}
}

ACTOR BiggerTracerBulletPistol : TracerBullet
{
	Speed 120
	Damage (30)
	//MissileType TracerTrailBigger
	MissileType None
	Scale 0.6
	Decal "PlasmaScorchLower"
	States
	{
	Spawn:
		TRAC C 1 BRIGHT
		Loop
	Death:
	Crash:
		HGFB A 0 A_SpawnItemEx("GunSmoke")
		HGFB A 2 BRIGHT A_SpawnDebris("TracerSpark")
		HGFB B 1 BRIGHT A_SetTranslucent(0.9,0)
		HGFB B 1 BRIGHT A_PlaySound("ricochet")
		HGFB C 2 BRIGHT
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("HitFlesh")
		Stop
	}
}

ACTOR TracerTrailBigger : TracerTrail
{
	Scale 0.6
	States
	{
	Spawn:
		TRAC C 1 BRIGHT A_FadeOut(0.3)
		Loop
	}
}

ACTOR EvenBiggerTracerBulletPistol : TracerBullet // Fires smaller tracer bullets on impact.
{
	Speed 120
	Damage (30)
	//MissileType TracerTrailBigger
	MissileType None
	Scale 0.6
	Decal "PlasmaScorchLower"
	DeathSound "ColaHandgun/Explode"
	States
	{
	Spawn:
		TRAC C 1 BRIGHT
		Loop
	Death:
	Crash:
	XDeath:
		HGFB AAAAAAAAAA 0 A_SpawnProjectile("TracerBulletFragment", 4.0, 0, random(90.0,270.0), CMF_ABSOLUTEPITCH|CMF_OFFSETPITCH|CMF_AIMDIRECTION, random(-40.0,40.0))
		HGFB A 2 BRIGHT A_SpawnDebris("TracerSpark")
		HGFB B 2 BRIGHT A_SetTranslucent(0.9,0)
		HGFB C 2 BRIGHT
		Stop
	}
}

ACTOR TracerBulletFragment : TracerBullet
{
	Speed 20
	Damage (6)
	MissileType TracerTrail
	Scale 0.5
	States
	{
	Spawn:
		TRAC B 1 BRIGHT A_FadeOut(0.15)
		Loop
	Death:
	Crash:
		PUFF A 2 BRIGHT A_SpawnDebris("TracerSpark")
		PUFF B 2 BRIGHT A_SetTranslucent(0.9,0)
		PUFF CD 2 BRIGHT
		Stop
	XDeath:
		PUFF A 0 A_PlaySound("HitFlesh")
		Stop
	}
}

ACTOR ColaHandgunBurst : Inventory
{
	Inventory.MaxAmount 3
}

ACTOR ColaHandgunFireCount : Inventory
{
	Inventory.MaxAmount 5
}

ACTOR ColaHandgun : ColaWeapon  replaces Pistol
{
	Weapon.AmmoType1 "ColaHandgunClip"
	Weapon.AmmoUse1 1
	Weapon.SlotNumber 1
	Weapon.AmmoType2 "ColaHandgunExp"
	Weapon.AmmoUse2 3
	Weapon.AmmoGive2 0
	Weapon.SisterWeapon "ColaHandgunSuper"
	Weapon.SelectionOrder 7500
	Inventory.PickupMessage "Marston R-1 - \"This town ain't big enough for the 12 of us!\""
	Inventory.PickupSound "ColaHandgun/Cylinder"
	Tag "Marston R-1"
	+Weapon.NoAutoFire
	+WEAPON.ALT_USES_BOTH
	+Weapon.Alt_Ammo_Optional
    Inventory.Icon "REVPA0"
	States
	{
	Spawn:
		REVP A -1
		Stop
	Select:
		RPIS A 0 A_GiveInventory("ColaHandgunSelected", 1)
		RPIS A 0 A_Raise
		RPIS A 1 A_Raise
		Loop
	Deselect:
		RPIS A 0 A_TakeInventory("ColaHandgunSelected", 999)
		RPIS A 1 {
			A_Lower;
			A_Lower;
			}
		Loop
	Ready:		
		RPIS A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	Ready2:
		RPIS F 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	Ready3:
		RPIS K 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	Ready4:
		RPIS P 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	Ready5:
		RPIS U 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	Ready6:
		RPIS Z 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
		
	Altfire: // Oh god I'm about to commit code crime
		RPIS A 0 A_JumpIfNoAmmo("Dryfire")
		RPIS A 0 A_TakeInventory("ColaHandgunExp", 3)
    AltfireRepeat:
		/*
		RPIS B 1 BRIGHT {
			A_Overlay(2, "OverlayFireCode", 1);
			A_WeaponOffset(1, 1, WOF_INTERPOLATE|WOF_ADD);
		}
		RPIS C 1 BRIGHT 
        RPIC C 0 A_JumpIfInventory("ColaHandgunClip", 1, "AltfireRepeat")
		RPIS C 1 BRIGHT {
			A_WeaponOffset(2,2, WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(pitch - (0.8 * CallACS("RecoilAmount")), SPF_INTERPOLATE);
		}
		RPIS D 1 {
			A_WeaponOffset(3,4, WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(pitch + (0.4 * CallACS("RecoilAmount")), SPF_INTERPOLATE);
		}
		RPIS E 1 {
			A_WeaponOffset(3,3, WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(pitch + (0.2 * CallACS("RecoilAmount")), SPF_INTERPOLATE);
		}
		RPIS E 1 {
			A_WeaponOffset(-1,-1, WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(pitch + (0.2 * CallACS("RecoilAmount")), SPF_INTERPOLATE);
		}
		RPIS E 1 A_WeaponOffset(-1,-1, WOF_INTERPOLATE|WOF_ADD)
		RPIS F 1 A_WeaponOffset(-2,-1, WOF_INTERPOLATE|WOF_ADD)
		RPIS F 1 A_WeaponOffset(-1,-1, WOF_INTERPOLATE|WOF_ADD)
		RPIS F 1 A_WeaponOffset(-1,-1, WOF_INTERPOLATE|WOF_ADD)
		RPIS A 1 A_WeaponOffset(-1,-1, WOF_INTERPOLATE|WOF_ADD)
		RPIS A 1 A_WeaponOffset(1,-1, WOF_INTERPOLATE|WOF_ADD)
        RPIS A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		*/
		RPIS B 1 BRIGHT A_Overlay(2, "OverlayFireCode", 1)
		RPIS B 1 BRIGHT
        RPIC C 0 A_JumpIfInventory("ColaHandgunClip", 1, "AltfireRepeat")
		RPIS C 2 BRIGHT
		RPIS D 2
		RPIS E 3
		RPIS F 3
        RPIS A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		Goto Ready
	Dryfire:
		RPIS A 1 Offset(1,34) A_PlaySound("weapons/dryfire", CHAN_AUTO)
		RPIS A 1 Offset(0,33)
		RPIS A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		Goto Ready
	OverlayFireCode:
		TNT1 A 0 {
			A_GunFlash;
			A_AlertMonsters;
			A_TakeInventory("ColaHandgunClip", 1);
			if ( CountInv("ColaHandgunLevel") == 3 )
			{
				A_PlaySound("ColaHandgun/FireTwiceBigger", CHAN_WEAPON);
				A_FireCustomMissile("EvenBiggerTracerBulletPistol", frandom(-0.5,0.5), 0, 9, 0, FPF_AIMATANGLE, frandom(-0.5,0.5));
			}
			else if ( CountInv("ColaHandgunLevel") == 2 )
			{
				A_PlaySound("ColaHandgun/FireBigger", CHAN_WEAPON);
				A_FireCustomMissile("BiggerTracerBulletPistol", frandom(-0.5,0.5), 0, 9, 0, FPF_AIMATANGLE, frandom(-0.5,0.5));
			}
			else if ( CountInv("ColaHandgunLevel") == 1 )
			{
				A_PlaySound("ColaHandgun/FireTwice", CHAN_WEAPON);
				A_FireCustomMissile("TracerBulletPistol", frandom(-0.5,0.5), 0, 9, 4, FPF_AIMATANGLE, frandom(-0.5,0.5));
				A_FireCustomMissile("TracerBulletPistol", frandom(-0.5,0.5), 0, 9, -2, FPF_AIMATANGLE, frandom(-0.5,0.5));
			}
			else if ( CountInv("ColaHandgunLevel") < 1 )
			{
				A_PlaySound("ColaHandgun/Fire", CHAN_WEAPON);
				A_FireCustomMissile("TracerBulletPistol", frandom(-0.5,0.5), 0, 9, 0, FPF_AIMATANGLE, frandom(-0.5,0.5));
			}
			if(GetCVAR("colavr_weapons_smoke"))
			{
				A_FireProjectile("GunBarrelSmoke",0,0,0,0,FPF_NOAUTOAIM,0);
			}
		}
		Stop
	Fire:
		RPIS A 0 A_JumpIfNoAmmo("Dryfire")
		
		RPIS A 0 A_JumpIfInventory("ColaHandgunFireCount", 5, "Fire6")
		RPIS A 0 A_JumpIfInventory("ColaHandgunFireCount", 4, "Fire5")
		RPIS A 0 A_JumpIfInventory("ColaHandgunFireCount", 3, "Fire4")
		RPIS A 0 A_JumpIfInventory("ColaHandgunFireCount", 2, "Fire3")
		RPIS A 0 A_JumpIfInventory("ColaHandgunFireCount", 1, "Fire2")
		RPIS A 0 A_GiveInventory("ColaHandgunFireCount", 1)
		
		RPIS B 1 BRIGHT A_Overlay(2, "OverlayFireCode", 1)
	FireContinue:
		/*
		RPIS C 1 BRIGHT {
			A_WeaponOffset(2,2, WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(pitch - (0.8 * CallACS("RecoilAmount")), SPF_INTERPOLATE);
		}
		RPIS D 1 {
			A_WeaponOffset(3,4, WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(pitch + (0.4 * CallACS("RecoilAmount")), SPF_INTERPOLATE);
		}
		RPIS E 1 {
			A_WeaponOffset(3,3, WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(pitch + (0.2 * CallACS("RecoilAmount")), SPF_INTERPOLATE);
		}
		RPIS E 1 {
			A_WeaponOffset(-1,-1, WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(pitch + (0.2 * CallACS("RecoilAmount")), SPF_INTERPOLATE);
		}
		RPIS E 1 A_WeaponOffset(-1,-1, WOF_INTERPOLATE|WOF_ADD)
		RPIS F 1 {
			A_WeaponOffset(-2,-1, WOF_INTERPOLATE|WOF_ADD);
			A_WeaponReady(WRF_NOBOB);
		}
		RPIS F 1 {
			A_WeaponOffset(-1,-1, WOF_INTERPOLATE|WOF_ADD);
			A_WeaponReady(WRF_NOBOB);
		}
		RPIS F 1 {
			A_WeaponOffset(-1,-1, WOF_INTERPOLATE|WOF_ADD);
			A_WeaponReady(WRF_NOBOB);
		}
		RPIS A 1 {
			A_WeaponOffset(-1,-1, WOF_INTERPOLATE|WOF_ADD);
			A_WeaponReady(WRF_NOBOB);
		}
		RPIS A 1 {
			A_WeaponOffset(1,-1, WOF_INTERPOLATE|WOF_ADD);
			A_WeaponReady(WRF_NOBOB);
		}
		*/
		RPIS B 1 BRIGHT
		RPIS C 2 BRIGHT
		RPIS D 2
		RPIS EE 1 A_WeaponReady(WRF_NOBOB)
		RPIS FFF 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready2
		
	Fire2:
		RPIS A 0 A_GiveInventory("ColaHandgunFireCount", 1)
		RPIS G 1 BRIGHT A_Overlay(2, "OverlayFireCode", 1)
		RPIS G 1 BRIGHT
		RPIS H 2 BRIGHT
		RPIS I 2
		RPIS JJ 1 A_WeaponReady(WRF_NOBOB)
		RPIS KKK 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready3
	Fire3:
		RPIS A 0 A_GiveInventory("ColaHandgunFireCount", 1)
		RPIS L 1 BRIGHT A_Overlay(2, "OverlayFireCode", 1)
		RPIS L 1 BRIGHT
		RPIS M 2 BRIGHT
		RPIS N 2
		RPIS OO 1 A_WeaponReady(WRF_NOBOB)
		RPIS PPP 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready4
	Fire4:
		RPIS A 0 A_GiveInventory("ColaHandgunFireCount", 1)
		RPIS Q 1 BRIGHT A_Overlay(2, "OverlayFireCode", 1)
		RPIS Q 1 BRIGHT
		RPIS R 2 BRIGHT 
		RPIS S 2
		RPIS TT 1 A_WeaponReady(WRF_NOBOB)
		RPIS UUU 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready5
	Fire5:
		RPIS A 0 A_GiveInventory("ColaHandgunFireCount", 1)
		RPIS V 1 BRIGHT A_Overlay(2, "OverlayFireCode", 1)
		RPIS V 1 BRIGHT
		RPIS W 2 BRIGHT
		RPIS X 2
		RPIS YY 1 A_WeaponReady(WRF_NOBOB)
		RPIS ZZZ 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready6
	Fire6:
		RPIS A 0 A_TakeInventory("ColaHandgunFireCount", 5)
		RPI2 A 1 BRIGHT A_Overlay(2, "OverlayFireCode", 1)
		RPI2 A 1 BRIGHT
		RPI2 B 2 BRIGHT
		RPI2 C 2
		RPI2 DD 1 A_WeaponReady(WRF_NOBOB)
		RPI2 EEE 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready
		
	Reload:
		RPIS A 0 A_JumpIfInventory("ColaHandgunClip", 0, 1)
		Goto Reload2
		RPIS A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready
	Reload2:
		/*
		RPIR A 1 Offset(1,39) A_PlaySound("ColaHandgun/cylinder", 6)
		RPIR A 1 Offset(2,37)
		RPIR A 1 Offset(3,35) {
            A_PlaySound("ColaHandgun/eject", 5);
            while ( CountInv("ColaHandgunClip") < 12 )
            {
                A_SpawnItemEx("PistolCasing", 0,0,20, random(-2,2),random(-2,2),0, random(1,359), SXF_NOCHECKPOSITION);
                A_Giveinventory("ColaHandgunClip", 1);
            }
        }
		RPIR B 1 Offset(5,35) A_Jump(128,2)
		RPIR B 1 Offset(7,37)
		RPIR B 1 Offset(8,39)
		RPIR B 1 Offset(9,42)
		RPIR B 1 Offset(9,44)
		RPIR B 1 Offset(8,46)
		RPIR B 1 Offset(8,49)
		RPIR B 1 Offset(7,54) A_PlaySound("ColaHandgun/insert", 6)
		RPIR B 1 Offset(6,59)
		RPIR B 1 Offset(5,65) A_Jump(128,2)
		RPIR B 1 Offset(4,70)
		RPIR B 1 Offset(3,77)
		RPIR B 1 Offset(2,73)
		RPIR B 1 Offset(2,69)
		RPIR B 1 Offset(1,65) 
		RPIR B 1 Offset(1,62) A_Jump(128,2)
		RPIR B 1 Offset(1,59)
		RPIR B 1 Offset(1,56)
		RPIR B 1 Offset(2,53)
		RPIR B 1 Offset(3,51)
		RPIR B 1 Offset(4,50) A_PlaySound("ColaHandgun/cylinder", 5)
		RPIR B 1 Offset(6,47) A_Jump(128,2)
		RPIR A 1 Offset(4,43)
		RPIR A 1 Offset(2,39)
		RPIR A 1 Offset(1,35)
		*/
		RPIR A 1 A_PlaySound("ColaHandgun/cylinder", 6)
		RPIR BC 1
		RPIR D 1 {
            A_PlaySound("ColaHandgun/eject", 5);
            while ( CountInv("ColaHandgunClip") < 12 )
            {
                A_SpawnItemEx("PistolCasing", 0,0,20, random(-2,2),random(-2,2),0, random(1,359), SXF_NOCHECKPOSITION);
                A_Giveinventory("ColaHandgunClip", 1);
            }
        }
		RPIR DEF 1
		RPIR G 3
		RPIR HIJ 1
		RPIR K 3 A_PlaySound("ColaHandgun/insert", 6)
		RPIR LM 1
		RPIR N 1 A_PlaySound("ColaHandgun/cylinder", 5)
		RPIR OPQRS 1
		RPIR T 1 A_Jump(128, "Ready")
		RPIR UVWX 1
		Goto Ready
	Flash:
	AltFlash:
		TNT1 A 0 A_JumpIf(GetCVAR("colavr_weapons_mf")==true, "MuzzleFlash")
		TNT1 A 1 Bright A_Light2
		TNT1 A 1 Bright A_Light1
		TNT1 A 1 Bright A_Light0
		Stop
	MuzzleFlash:
		RPMF A 1 Bright A_Light2
		RPMF B 1 Bright A_Light1
		RPMF C 1 Bright A_Light0
		Stop
	}
}

ACTOR ColaHandgunSuper : ColaHandgun
{
	Weapon.SisterWeapon "ColaHandgun"
	+POWERED_UP
	States
	{
	OverlayFireCode:
		TNT1 A 0 {
			A_GunFlash;
			A_AlertMonsters;
			A_PlaySound("ColaHandgun/FireTwiceBigger", CHAN_WEAPON);
			A_FireCustomMissile("EvenBiggerTracerBulletPistol", frandom(0.5,2.5), 1, 9, 0, FPF_AIMATANGLE, frandom(-0.5,0.5));
			A_FireCustomMissile("EvenBiggerTracerBulletPistol", frandom(-2.5,-0.5), 0, 9, 0, FPF_AIMATANGLE, frandom(-0.5,0.5));
		}
		Stop
	}
}

//Cheat stuff, for developing purpose
Actor ColaBubblesCheat
{
	States
	{
	Spawn:
		TNT1 A 0 Nodelay A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		TNT1 A 1 A_DropItem("ColaBubblePickup")
		Stop
	}
}