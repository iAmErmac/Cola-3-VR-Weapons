ACTOR CardThrowerSelected : Inventory {}

ACTOR CardThrowerLevel : Inventory
{
	Inventory.MaxAmount 3
}

ACTOR CardThrowerExp : Ammo
{
	Inventory.MaxAmount 15
	+IGNORESKILL
}

ACTOR ThrownCard
{
	Scale 0.1
	Radius 8
	Height 4
	Speed 20
	Gravity 0.1
	Damage (8)
	PROJECTILE
	-NoGravity
	-NoBlockMap
	Mass 1
	DeathSound "Cards/Hit"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256, "Beach", "Castle", "Fan", "Fish", "Robot", "Tornado")
	Beach:
		TCD1 A 1 A_Jump(256, "FlyLoop")
	Castle:
		TCD2 A 1 A_Jump(256, "FlyLoop")
	Fan:
		TCD3 A 1 A_Jump(256, "FlyLoop")
	Fish:
		TCD4 A 1 A_Jump(256, "FlyLoop")
	Robot:
		TCD5 A 1 A_Jump(256, "FlyLoop")
	Tornado:
		TCD6 A 1 A_Jump(256, "FlyLoop")
	FlyLoop:
		"####" BCDEFGHIJKLMNOA 1
		Loop
	Death:
		"####" "#" 0 A_Scream
		"####" "#" 0 A_SetGravity(1.0)
		"####" "#" 150
		"####" "#" 1 A_FadeOut(0.01)
		Wait
	}
}

ACTOR ThrownCardTrail
{
	RenderStyle Stencil
	StencilColor "FF 00 FF"
	Alpha 0.9
	Scale 0.1
	+NoGravity
	States
	{
	Spawn:
		"####" "#" 1 NoDelay Bright A_FadeOut(0.1)
		Wait
	}
}

ACTOR FastThrownCard : ThrownCard
{
	Speed 30
	Damage (12)
	States
	{
	FlyLoop:
		"####" BCDEFGHIJKLMNOA 1 A_SpawnItemEx("ThrownCardTrail", 0,0,0,0,0,0,0, SXF_TRANSFERSPRITEFRAME)
		Loop
	}
}

ACTOR SuperFastThrownCard : ThrownCard
{
	Speed 40
	Damage (20)
	BounceType Hexen
	WallBounceFactor 1.0
	BounceFactor 1.0
	BounceCount 3
	DeathSound "Cards/SuperHit"
	States
	{
	FlyLoop:
		"####" BCDEFGHIJKLMNOA 1 A_SpawnItemEx("ThrownCardTrail", 0,0,0,0,0,0,0, SXF_TRANSFERSPRITEFRAME)
		Loop
	Death:
		"####" "#" 0 A_Scream
		"####" "#" 0 A_SetGravity(1.0)
		"####" "#" 0 A_SpawnDebris("BouncySolitaireCard")
		"####" "#" 150
		"####" "#" 1 A_FadeOut(0.01)
		Wait
	}
}

ACTOR CardThrower : ColaWeapon replaces Chainsaw
{
	Weapon.SlotNumber 3
	Weapon.AmmoType1 "CardThrowerExp"
	Weapon.AmmoGive1 0
	Weapon.AmmoUse1 0
	Weapon.AmmoType2 "CardThrowerExp"
	Weapon.AmmoGive2 0
	Weapon.AmmoUse2 1
	Weapon.SisterWeapon "CardThrowerSuper"
	Inventory.PickupMessage "Kare Brand Cards - \"Give your game an edge!\" (Slot 3)"
	Inventory.PickupSound "Cards/Pickup"
	Tag "Kare Brand Cards"
	+Weapon.Alt_Ammo_Optional
	+Weapon.NoAutofire
    Inventory.Icon "WSOLA0"
	Scale 0.6
	States
	{
	Spawn:
		WSOL A -1
		Stop
	Select:
		VDCK A 0 A_GiveInventory("CardThrowerSelected", 1)
		VDCK A 1 {
			A_Raise;
			A_Raise;
		}
		Loop
	Deselect:
		VDCK A 0 A_TakeInventory("CardThrowerSelected", 999)
		VDCK A 1 {
			A_Lower;
			A_Lower;
			}
		Loop
	Ready:
		VDCK A 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
	ReadyDelay:
		TNT1 A 8
		Goto Ready
		
	Fire:
		VDCK A 0 A_JumpIfInventory("CardThrowerLevel", 3, "Fire0")
		VDCK A 0 A_JumpIfInventory("CardThrowerLevel", 2, "Fire1")
		VDCK A 0 A_JumpIfInventory("CardThrowerLevel", 1, "Fire2")
		VDCK A 0 A_Jump(256,"Fire3")
	Fire0:
		VDCK B 1
		VDCK C 1 A_PlaySound("Cards/Throw")
		VDCK D 1 A_FireCustomMissile("ThrownCard", frandom(-5.0, 5.0))
		VDCK E 1
		Goto BringUp
	Fire1:
		VDCK B 1
		VDCK C 1 A_PlaySound("Cards/Throw")
		VDCK D 0 A_FireCustomMissile("ThrownCard", frandom(1.0, 5.0))
		VDCK D 1 A_FireCustomMissile("ThrownCard", frandom(-5.0, -1.0))
		VDCK E 1
		Goto BringUp
	Fire2:
		VDCK B 1
		VDCK C 1 A_PlaySound("Cards/SuperThrow")
		VDCK D 0 A_FireCustomMissile("FastThrownCard", frandom(2.0, 6.0))
		VDCK D 0 A_FireCustomMissile("FastThrownCard", frandom(-2.0, 2.0))
		VDCK D 1 A_FireCustomMissile("FastThrownCard", frandom(-6.0, -2.0))
		VDCK E 1
		Goto BringUp
	Fire3:
		VDCK B 1
		VDCK C 1 A_PlaySound("Cards/SuperThrow")
		VDCK D 0 A_FireCustomMissile("SuperFastThrownCard", frandom(2.0, 6.0))
		VDCK D 0 A_FireCustomMissile("SuperFastThrownCard", frandom(-2.0, 2.0))
		VDCK D 1 A_FireCustomMissile("SuperFastThrownCard", frandom(-6.0, -2.0))
		VDCK E 1
		Goto BringUp
	BringUp:
		/*
		TNT1 A 6 A_WeaponOffset(0, 96, WOF_KEEPX)
		VDCK AAAA 1 A_WeaponOffset(0, -16, WOF_KEEPX|WOF_INTERPOLATE|WOF_ADD)
		*/
		VDCK E 6
		VDCK DCBA 1
		VDCK A 0 A_Refire
		Goto Ready
	Altfire:
		VDCK A 0 A_JumpIfNoAmmo("Dryfire")
		VDCK A 0 A_JumpIfInventory("CardThrowerLevel", 3, "AltFire3")
		VDCK A 0 A_JumpIfInventory("CardThrowerLevel", 2, "AltFire2")
		VDCK A 0 A_JumpIfInventory("CardThrowerLevel", 1, "AltFire1")
		VDCK B 1
		VDCK C 1 {
            A_PlaySound("Cards/Showdown!", CHAN_VOICE, 1.0, 0, ATTN_NONE);
			A_PlaySound("Cards/Throw");
			A_TakeInventory("CardThrowerExp", 999);
			A_TakeInventory("CardThrowerLevel", 999);
			A_FireCustomMissile("PokerCardLevel0", -12.5, 0);
			A_FireCustomMissile("PokerCardLevel0", -6.25, 0);
			A_FireCustomMissile("PokerCardLevel0", 0, 0);
			A_FireCustomMissile("PokerCardLevel0", 6.25, 0);
			A_FireCustomMissile("PokerCardLevel0", 12.5, 0);
		}
		VDCK DE 1
		Goto BringUp
	AltFire1:
		VDCK B 1
		VDCK C 1 {
            A_PlaySound("Cards/Showdown!", CHAN_VOICE, 1.0, 0, ATTN_NONE);
			A_PlaySound("Cards/Throw");
			A_TakeInventory("CardThrowerExp", 999);
			A_TakeInventory("CardThrowerLevel", 999);
			A_FireCustomMissile("PokerCardLevel1Card1", -12.5, 0);
			A_FireCustomMissile("PokerCardLevel1Card2", -6.25, 0);
			A_FireCustomMissile("PokerCardLevel1Card3", 0, 0);
			A_FireCustomMissile("PokerCardLevel1Card4", 6.25, 0);
			A_FireCustomMissile("PokerCardLevel1Card5", 12.5, 0);
		}
		VDCK DE 1
		Goto BringUp
	AltFire2:
		VDCK B 1
		VDCK C 1 {
			A_PlaySound("Cards/Showdown!", CHAN_VOICE, 1.0, 0, ATTN_NONE);
			A_PlaySound("Cards/SuperThrow", CHAN_WEAPON);
			A_TakeInventory("CardThrowerExp", 999);
			A_TakeInventory("CardThrowerLevel", 999);
			A_FireCustomMissile("PokerCardLevel2Card1", -12.5, 0);
			A_FireCustomMissile("PokerCardLevel2Card2", -6.25, 0);
			A_FireCustomMissile("PokerCardLevel2Card3", 0, 0);
			A_FireCustomMissile("PokerCardLevel2Card4", 6.25, 0);
			A_FireCustomMissile("PokerCardLevel2Card5", 12.5, 0);
		}
		VDCK DE 1
		Goto BringUp
	AltFire3:
		VDCK B 1
		VDCK C 1 {
            A_PlaySound("Cards/Royalflush", CHAN_VOICE, 1.0, 0, ATTN_NONE);
			A_PlaySound("Cards/SuperThrow", CHAN_WEAPON);
			A_TakeInventory("CardThrowerExp", 999);
			A_TakeInventory("CardThrowerLevel", 999);
			A_FireCustomMissile("PokerCardLevel3Card1", -12.5, 0);
			A_FireCustomMissile("PokerCardLevel3Card2", -6.25, 0);
			A_FireCustomMissile("PokerCardLevel3Card3", 0, 0);
			A_FireCustomMissile("PokerCardLevel3Card4", 6.25, 0);
			A_FireCustomMissile("PokerCardLevel3Card5", 12.5, 0);
		}
		VDCK DE 1
		Goto BringUp
	Dryfire:
		VDCK A 1 A_PlaySound("weapons/carddryfire", CHAN_WEAPON)
		VDCE ABCCCCCBA 1
		Goto Ready
	}
}

ACTOR CardThrowerSuper : CardThrower
{
	Weapon.SisterWeapon "CardThrower"
	+POWERED_UP
	States
	{
	Fire:
		/*
		VDCK AAAA 1 A_WeaponOffset(-4, 16, WOF_INTERPOLATE|WOF_ADD)
		TNT1 A 2
		VDCX AAAA 1 A_WeaponOffset(4, -16, WOF_KEEPX|WOF_INTERPOLATE|WOF_ADD)
		*/
		VDCK BCDEF 2
	Hold:
		VDCX A 1 //A_WeaponOffset(0, 32, WOF_INTERPOLATE)
		VDCX A 1 {
			A_PlaySound("Cards/Streamchord", CHAN_WEAPON);
			//A_WeaponOffset(frandom(4.0, 10.0), frandom(34.0, 44.0), WOF_INTERPOLATE);
			A_FireCustomMissile("FastThrownCard", frandom(-2.0, 2.0), 0, 8, frandom(-2.0, 2.0));
		}
		VDCX A 1 A_Refire
		VDCX AAAA 1 //A_WeaponOffset(-4, 16, WOF_INTERPOLATE|WOF_ADD)
		//TNT1 A 2
		VDCX A 2
		VDCK AAAA 1 //A_WeaponOffset(4, -16, WOF_KEEPX|WOF_INTERPOLATE|WOF_ADD)
		Goto Ready
	AltFire:
		VDCK B 1
		VDCK C 1 A_PlaySound("Cards/SuperThrow")
		VDCK D 1 A_FireCustomMissile("ThrownRobot", 0, 1)
		VDCK E 1
		Goto BringUp
	BringUp:
		/*
		TNT1 A 6 A_WeaponOffset(0, 96, WOF_KEEPX)
		VDCK AAAA 1 A_WeaponOffset(0, -16, WOF_KEEPX|WOF_INTERPOLATE|WOF_ADD)
		*/
		VDCK E 6
		VDCK DCBA 1
		Goto Ready
	}
}

ACTOR BouncySolitaireCard // With apologies to Isle, whose code I didn't borrow, but he already had the cards in sprite form...
{
	Health 5
	Scale 0.075
	Speed 4
	Radius 1
	Height 1
	+NoTeleport
	-NoBlockmap
	-NoGravity
	DeathSound "Cards/Hit"
	BounceSound "Cards/Hit"
	Mass 1
	States
	{
	Spawn:
		TNT1 AAAAAA 0 NoDelay A_Jump(256, "Spades", "Hearts", "Clubs", "Diamonds")
	Spades:
		TNT1 A 0 A_Jump(256, random(1,13))
		SPDS ABCDEFGHIJKLM 1 A_Jump(256, "WaitItOut")
	Hearts:
		TNT1 A 0 A_Jump(256, random(1,13))
		HRTS ABCDEFGHIJKLM 1 A_Jump(256, "WaitItOut")
	Clubs:
		TNT1 A 0 A_Jump(256, random(1,13))
		CLBS ABCDEFGHIJKLM 1 A_Jump(256, "WaitItOut")
	Diamonds:
		TNT1 A 0 A_Jump(256, random(1,13))
		DMNS ABCDEFGHIJKLM 1 A_Jump(256, "WaitItOut")
	WaitItOut:
		"####" "#" 1 A_SpawnItemEx("SolitaireCardTrail", 0,0,0,0,0,0,0, SXF_TRANSFERSPRITEFRAME)
		"####" "#" 0 A_Jump(16, "Death")
		Loop
	Death:
		"####" "#" 0 A_Scream
		"####" "#" 70
		"####" "#" 1 A_FadeOut(0.05)
		Wait
	}
}

ACTOR SolitaireCardTrail
{
	Scale 0.075
	+NoGravity
	States
	{
	Spawn:
		"####" "#" 20
		"####" "#" 1 A_FadeOut(0.1)
		Wait
	}
}

// Poker cards for altfire

ACTOR PokerCardLevel0 : ThrownCard
{
	Scale 0.25
	States
	{
	Death:
		TNT1 A 0 A_SetGravity(0.0)
		TNT1 A 0 A_Jump(256, "Spades", "Hearts", "Clubs", "Diamonds")
	Spades:
		TNT1 A 0 A_Jump(256, random(1,13))
		SPDS ABCDEFGHIJKLM 1 A_Jump(256, "WaitItOut")
	Hearts:
		TNT1 A 0 A_Jump(256, random(1,13))
		HRTS ABCDEFGHIJKLM 1 A_Jump(256, "WaitItOut")
	Clubs:
		TNT1 A 0 A_Jump(256, random(1,13))
		CLBS ABCDEFGHIJKLM 1 A_Jump(256, "WaitItOut")
	Diamonds:
		TNT1 A 0 A_Jump(256, random(1,13))
		DMNS ABCDEFGHIJKLM 1 A_Jump(256, "WaitItOut")
	WaitItOut:
		"####" "#" 35
		"####" "#" 0 A_SetGravity(1.0)
		"####" "#" 70 A_Scream
		"####" "#" 1 A_FadeOut(0.05)
		Wait
	}
}

// Level 1 effect - card 3 spawns the winning sign

ACTOR PokerWinLevel1
{
	+NoGravity
	+NoInteraction
	Scale 0.5
	States
	{
	Spawn:
		PKR1 A 1 BRIGHT
		PKR1 A 1 BRIGHT A_PlaySound("Cards/PokerWin1", CHAN_BODY, 2.0)
		PKR1 A 35 BRIGHT
		"####" "#" 1 BRIGHT A_FadeOut(0.05)
		Wait
	}
}

ACTOR PokerCardLevel1Card1 : ThrownCard
{
	Scale 0.25
	States
	{
	Death:
		SPDS A 1 BRIGHT
		Goto PokerFadeOut
	PokerFadeOut:
		"####" "#" 0 A_SetGravity(0.0)
		"####" "#" 50 BRIGHT
		FRME A 0 A_SetScale(0.5, 0.5)
		FRME A 2 BRIGHT A_PlaySound("Piledriver/Explode")
		FRME B 2 BRIGHT A_Explode(32, 128, 0)
		FRME CDEFGHIJ 2 BRIGHT
		Stop
	}
}

ACTOR PokerCardLevel1Card2 : PokerCardLevel1Card1
{
	Scale 0.25
	States
	{
	Death:
		HRTS B 1 BRIGHT
		Goto PokerFadeOut
	}
}

ACTOR PokerCardLevel1Card3 : PokerCardLevel1Card1
{
	Scale 0.25
	States
	{
	Death:
		CLBS C 1 BRIGHT A_SpawnItemEx("PokerWinLevel1", 0,0,0, 0,0,0.5)
		Goto PokerFadeOut
	}
}

ACTOR PokerCardLevel1Card4 : PokerCardLevel1Card1
{
	Scale 0.25
	States
	{
	Death:
		SPDS D 1 BRIGHT
		Goto PokerFadeOut
	}
}

ACTOR PokerCardLevel1Card5 : PokerCardLevel1Card1
{
	Scale 0.25
	States
	{
	Death:
		DMNS E 1 BRIGHT
		Goto PokerFadeOut
	}
}

// Level 2 effect - card 3 spawns the winning sign

ACTOR PokerWinLevel2
{
	+NoGravity
	+NoInteraction
	Scale 0.5
	States
	{
	Spawn:
		PKR2 A 1 BRIGHT
		PKR2 A 1 BRIGHT A_PlaySound("Cards/PokerWin2", CHAN_BODY, 2.0)
		PKR2 A 35 BRIGHT
		"####" "#" 1 A_FadeOut(0.05)
		Wait
	}
}

ACTOR PokerCardLevel2Card1 : ThrownCard
{
	Scale 0.25
	States
	{
	Death:
		SPDS A 1 BRIGHT
		Goto PokerFadeOut
	PokerFadeOut:
		"####" "#" 0 A_SetGravity(0.0)
		"####" "#" 50 BRIGHT
		FRME A 0 A_SetScale(0.5, 0.5)
		FRME A 0 A_PlaySound("Piledriver/Explode")
		FRME BBB 0 A_SpawnItemEx("SodaGrenadeThrown", 0,0,8, 0,0,0, random(0,359))
		FRME B 2 BRIGHT A_Explode(128, 160, 0)
		FRME BCDEFGHIJ 2 BRIGHT
		Stop
	}
}

ACTOR PokerCardLevel2Card2 : PokerCardLevel2Card1
{
	Scale 0.25
	States
	{
	Death:
		HRTS H 1 BRIGHT
		Goto PokerFadeOut
	}
}

ACTOR PokerCardLevel2Card3 : PokerCardLevel2Card1
{
	Scale 0.25
	States
	{
	Death:
		CLBS A 1 BRIGHT A_SpawnItemEx("PokerWinLevel2", 0,0,0, 0,0,0.5)
		Goto PokerFadeOut
	}
}

ACTOR PokerCardLevel2Card4 : PokerCardLevel2Card1
{
	Scale 0.25
	States
	{
	Death:
		SPDS H 1 BRIGHT
		Goto PokerFadeOut
	}
}

ACTOR PokerCardLevel2Card5 : PokerCardLevel2Card1
{
	Scale 0.25
	States
	{
	Death:
		CLBS H 1 BRIGHT
		Goto PokerFadeOut
	}
}

// Level 3 effect - poker chips everywhere!

ACTOR HurtyPokerChips
{
	Projectile
	-NoGravity
	-NoBlockmap
	+BounceOnActors
	+BounceOnWalls
	BounceFactor 0.3
	DamageType HurtyPokerChips
	BounceSound "Pokerchips/Hit"
	DeathSound "Pokerchips/Hit"
	Speed 30
	Damage (15)
	States
	{
	Spawn:
		PKRC ABCDEFABCDEFABCDEFABCDEFABCDEF 2
		PKRC ABCDEF 1
		PKRC A 2 A_Die
		Loop
	Death:
		PKRC G 200 A_Scream
		PKRC G 1 A_FadeOut(0.05)
		Wait
	}
}

/*
ACTOR PokerWinLevel3
{
	+NoGravity
	+NoInteraction
	Scale 0.5
	ReactionTime 150
	var int user_ThrowCardAngle;
	States
	{
	Spawn:
		PKR3 A 1 BRIGHT NoDelay A_SetUserVar("user_ThrowCardAngle",0)
		PKR3 A 1 BRIGHT A_PlaySound("Cards/PokerWin3", CHAN_BODY, 2.0)
	WinLoop:
		PKR3 A 1 BRIGHT {
			A_Countdown;
			A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-30.0, 15.0));
			A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+45, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-15.0, 15.0));
			A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+90, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-30.0, 15.0));
			A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+135, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-15.0, 15.0));
			A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+180, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-30.0, 15.0));
			A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+225, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-15.0, 15.0));
			A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+270, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-30.0, 15.0));
			A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+315, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-15.0, 15.0));
		}
		Loop
	Death:
		PKR3 A 35 BRIGHT
		"####" "#" 1 A_FadeOut(0.05)
		Wait
	}
}
*/

//Refactoring this to address the terrible lag on QuestZDoom
ACTOR PokerWinLevel3
{
	+NoGravity
	+NoInteraction
	Scale 0.5
	ReactionTime 15
	var int user_ThrowCardAngle;
	States
	{
	Spawn:
		PKR3 A 1 BRIGHT NoDelay A_SetUserVar("user_ThrowCardAngle",0)
		PKR3 A 1 BRIGHT A_PlaySound("Cards/PokerWin3", CHAN_BODY, 2.0)
		PKR3 A 0 A_SpawnItem("InvisChipsTracerSpawner")
	WinLoop:
		PKR3 A 1 BRIGHT A_Countdown
		PKR3 A 1 BRIGHT A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+(random(1,7)*45), CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-30.0, 15.0))
		PKR3 A 1 BRIGHT A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+(random(1,7)*45), CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-15.0, 15.0))
		PKR3 A 1 BRIGHT A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+(random(1,7)*45), CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-15.0, 15.0))
		PKR3 A 1 BRIGHT A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+(random(1,7)*45), CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-15.0, 15.0))
		PKR3 A 1 BRIGHT A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+(random(1,7)*45), CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-15.0, 15.0))
		PKR3 A 1 BRIGHT A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+(random(1,7)*45), CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-15.0, 15.0))
		PKR3 A 1 BRIGHT A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+(random(1,7)*45), CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-15.0, 15.0))
		PKR3 A 1 BRIGHT A_SpawnProjectile("HurtyPokerChips", -10, 0, user_ThrowCardAngle+(random(1,7)*45), CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE|CMF_TRACKOWNER, frandom(-15.0, 15.0))
		PKR3 A 1 BRIGHT A_SetUserVar("user_ThrowCardAngle",user_ThrowCardAngle+12)
		Loop
	Death:
		PKR3 A 35 BRIGHT
		"####" "#" 1 A_FadeOut(0.05)
		Wait
	}
}

Actor InvisChipsTracerSpawner //BFG like tracer to kill the surrounding monsters
{
	States
	{
	Spawn:
		TNT1 A 5
		TNT1 A 0 A_SpawnItemEx("HurtyPokerChipsTracer1", 0, 0, 5)
		TNT1 A 35
		TNT1 A 0 A_SpawnItemEx("HurtyPokerChipsTracer2", 0, 0, 10)
		TNT1 A 35
		TNT1 A 0 A_SpawnItemEx("HurtyPokerChipsTracer3", 0, 0, 15)
		TNT1 A 35
		TNT1 A 0 A_SpawnItemEx("HurtyPokerChipsTracer4", 0, 0, 20)
		TNT1 A 35
		TNT1 A 0 A_SpawnItemEx("HurtyPokerChipsTracer5", 0, 0, 25)
		Stop
	}
}

Actor HurtyPokerChipsTracer1
{
	States
	{
	Spawn:
		TNT1 A 0 Nodelay A_BFGSpray("HurtyPokerChips", 5)
		Stop
	}
}

Actor HurtyPokerChipsTracer2
{
	States
	{
	Spawn:
		TNT1 A 0 Nodelay A_BFGSpray("HurtyPokerChips", 10)
		Stop
	}
}

Actor HurtyPokerChipsTracer3
{
	States
	{
	Spawn:
		TNT1 A 0 Nodelay A_BFGSpray("HurtyPokerChips", 20)
		Stop
	}
}

Actor HurtyPokerChipsTracer4
{
	States
	{
	Spawn:
		TNT1 A 0 Nodelay A_BFGSpray("HurtyPokerChips", 30)
		Stop
	}
}

Actor HurtyPokerChipsTracer5
{
	States
	{
	Spawn:
		TNT1 A 0 Nodelay A_BFGSpray("HurtyPokerChips", 40)
		Stop
	}
}

ACTOR PokerCardLevel3Card1 : ThrownCard
{
	Scale 0.25
	States
	{
	Death:
		SPDS J 1 BRIGHT
		Goto PokerFadeOut
	PokerFadeOut:
		"####" "#" 0 A_SetGravity(0.0)
		"####" "#" 100 BRIGHT
		FRME A 0 A_SetScale(0.5, 0.5)
		FRME A 0 A_PlaySound("Piledriver/Explode")
		FRME BBBBBB 0 A_SpawnProjectile("BouncySolitairecard", 0, 0, frandom(0.0, 359.0), CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE, frandom(0.0, 15.0))
		FRME BBB 0 A_SpawnItemEx("SodaGrenadeThrown", 0,0,8, 0,0,0, random(0,359))
		FRME B 2 BRIGHT A_Explode(128, 160, 0)
		FRME BCDEFGHIJ 2 BRIGHT
		Stop
	}
}

ACTOR PokerCardLevel3Card2 : PokerCardLevel3Card1
{
	Scale 0.25
	States
	{
	Death:
		SPDS K 1 BRIGHT
		Goto PokerFadeOut
	}
}

ACTOR PokerCardLevel3Card3 : PokerCardLevel3Card1
{
	Scale 0.25
	States
	{
	Death:
		SPDS L 1 BRIGHT A_SpawnItemEx("PokerWinLevel3", 0,0,0, 0,0,0.5)
		Goto PokerFadeOut
	}
}

ACTOR PokerCardLevel3Card4 : PokerCardLevel3Card1
{
	Scale 0.25
	States
	{
	Death:
		SPDS M 1 BRIGHT
		Goto PokerFadeOut
	}
}

ACTOR PokerCardLevel3Card5 : PokerCardLevel3Card1
{
	Scale 0.25
	States
	{
	Death:
		SPDS A 1 BRIGHT
		Goto PokerFadeOut
	}
}

// Level X Primary Fire

ACTOR ThrownRobot
{
	Scale 0.5
	Radius 16
	Height 32
	Speed 6
	Damage (8)
	Mass 1
	var int user_ThrowCardAngle;
	StencilColor "FFFFFF"
	maxStepHeight 24
	PROJECTILE
	-NoGravity
	-NoBlockMap
	+MOVEWITHSECTOR
	+FLOORHUGGER
	States
	{
	Spawn:
		SOLR A 0 NoDelay A_SetUserVar("user_ThrowCardAngle",0)
	WalkLoop:
		SOLR A 0 A_PlaySound("Cards/RobotStep", CHAN_BODY)
		SOLR AA 3 {
			A_PlaySound("Cards/Throw", CHAN_WEAPON);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+90, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+180, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+270, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SetUserVar("user_ThrowCardAngle",user_ThrowCardAngle+12);
		}
		SOLR B 0 A_PlaySound("Cards/RobotLeg1", CHAN_BODY, 0.2)
		SOLR BB 3 {
			A_PlaySound("Cards/Throw", CHAN_WEAPON);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+90, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+180, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+270, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SetUserVar("user_ThrowCardAngle",user_ThrowCardAngle+12);
		}
		SOLR A 0 A_PlaySound("Cards/RobotStep", CHAN_BODY)
		SOLR AA 3 {
			A_PlaySound("Cards/Throw", CHAN_WEAPON);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+90, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+180, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+270, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SetUserVar("user_ThrowCardAngle",user_ThrowCardAngle+12);
		}
		SOLR C 0 A_PlaySound("Cards/RobotLeg2", CHAN_BODY, 0.2)
		SOLR CC 3 {
			A_PlaySound("Cards/Throw", CHAN_WEAPON);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+90, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+180, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SpawnProjectile("FastThrownCard", 30.0, 0, user_ThrowCardAngle+270, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			A_SetUserVar("user_ThrowCardAngle",user_ThrowCardAngle+12);
		}
		Loop
	Death:
		SOLR A 0 A_PlaySound("Cards/RobotSpark", CHAN_BODY)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Stencil)
		SOLR A 10 A_SetRenderStyle(1.0, STYLE_Normal)
		SOLR A 0 A_PlaySound("Cards/RobotSpark", CHAN_BODY)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Stencil)
		SOLR A 8 A_SetRenderStyle(1.0, STYLE_Normal)
		SOLR A 0 A_PlaySound("Cards/RobotSpark", CHAN_BODY)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Stencil)
		SOLR A 6 A_SetRenderStyle(1.0, STYLE_Normal)
		SOLR A 0 A_PlaySound("Cards/RobotSpark", CHAN_BODY)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Stencil)
		SOLR A 4 A_SetRenderStyle(1.0, STYLE_Normal)
		SOLR A 0 A_PlaySound("Cards/RobotSpark", CHAN_BODY)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Stencil)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Normal)
		SOLR A 0 A_PlaySound("Cards/RobotSpark", CHAN_BODY)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Stencil)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Normal)
		SOLR A 0 A_PlaySound("Cards/RobotSpark", CHAN_BODY)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Stencil)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Normal)
		SOLR A 0 A_PlaySound("Cards/RobotSpark", CHAN_BODY)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Stencil)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Normal)
		SOLR A 0 A_PlaySound("Cards/RobotSpark", CHAN_BODY)
		SOLR A 2 A_SetRenderStyle(1.0, STYLE_Stencil)
		SOLR A 0 A_SpawnItemEx("ThrownRobotArm",	0,0,16, random(1,4), random(2,5), random(4,8), random(0,359))
		SOLR A 0 A_SpawnItemEx("ThrownRobotArm",	0,0,16, random(1,4), random(2,5), random(4,8), random(0,359))
		SOLR A 0 A_SpawnItemEx("ThrownRobotHead",	0,0,16, random(1,4), random(2,5), random(4,8), random(0,359))
		SOLR A 0 A_SpawnItemEx("ThrownRobotLeg",	0,0,16, random(1,4), random(2,5), random(4,8), random(0,359))
		SOLR A 0 A_SpawnItemEx("ThrownRobotLeg",	0,0,16, random(1,4), random(2,5), random(4,8), random(0,359))
		SOLR A 0 A_SpawnItemEx("ThrownRobotExplosion")
		Stop
	}
}

ACTOR ThrownRobotExplosion
{
	+NoGravity
	DeathSound "Piledriver/explode"
	DamageType Fire
	Scale 0.75
	States
	{
	Spawn:
		FRME A 2 BRIGHT Nodelay A_Scream
		FRME B 2 BRIGHT A_Explode(128, 128, 0)
		FRME CDEFGHIJ 2 BRIGHT
		Stop
	}
}

ACTOR ThrownRobotArm
{
	Radius 4
	Height 8
	Scale 0.5
	States
	{
	Spawn:
		SOLR D 105
		SOLR D 1 A_FadeOut(0.05)
		Wait
	}
}

ACTOR ThrownRobotHead
{
	Radius 4
	Height 8
	Scale 0.5
	States
	{
	Spawn:
		SOLR E 105
		SOLR E 1 A_FadeOut(0.05)
		Wait
	}
}

ACTOR ThrownRobotLeg
{
	Radius 4
	Height 8
	Scale 0.5
	States
	{
	Spawn:
		SOLR G 105
		SOLR G 1 A_FadeOut(0.05)
		Wait
	}
}